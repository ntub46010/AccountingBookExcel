# 工作底稿
>以工作底稿的格式製作總額式試算表、餘額式試算表、綜合損益表、資產負債表
>
>下方有「本期損益」欄位，顯示目前的收入扣掉支出是否有剩餘
>
>日常記帳時，每月底做結帳工作前可複製一份到另外的工作表作當月的保存，工作底稿在結帳後則繼續使用

<br>

## 使用畫面
<img src="https://github.com/ntub46010/AccountingBook/blob/master/%E4%BD%BF%E7%94%A8%E7%95%AB%E9%9D%A2/%E5%B7%A5%E4%BD%9C%E5%BA%95%E7%A8%BF(%E5%89%8D%E5%8D%8A%E9%83%A8).png" />
<br>
<img src="https://github.com/ntub46010/AccountingBook/blob/master/%E4%BD%BF%E7%94%A8%E7%95%AB%E9%9D%A2/%E5%B7%A5%E4%BD%9C%E5%BA%95%E7%A8%BF(%E5%BE%8C%E5%8D%8A%E9%83%A8).png" />

<br>

### 刷新工作底稿
    Sub 刷新工作底稿()
    Dim count1, count2, RowLimit, i, j, k, r As Integer
    Dim debit, credit As Single
    Dim column(11, 2), total(7) As String
    Dim find As Byte

    '科目0、借方1、貸方2
    column(0, 0) = "F": column(0, 1) = "G": column(0, 2) = "H"
    column(1, 0) = "O": column(1, 1) = "P": column(1, 2) = "Q"
    column(2, 0) = "X": column(2, 1) = "Y": column(2, 2) = "Z"
    column(3, 0) = "AG": column(3, 1) = "AH": column(3, 2) = "AI"
    column(4, 0) = "AP": column(4, 1) = "AQ": column(4, 2) = "AR"
    column(5, 0) = "AY": column(5, 1) = "AZ": column(5, 2) = "BA"
    column(6, 0) = "BH": column(6, 1) = "BI": column(6, 2) = "BJ"
    column(7, 0) = "BQ": column(7, 1) = "BR": column(7, 2) = "BS"
    column(8, 0) = "BZ": column(8, 1) = "CA": column(8, 2) = "CB"
    column(9, 0) = "CI": column(9, 1) = "CJ": column(9, 2) = "CK"
    column(10, 0) = "CR": column(10, 1) = "CS": column(10, 2) = "CT"
    column(11, 0) = "DA": column(11, 1) = "DB": column(11, 2) = "DC"

    Rows("9:9").RowHeight = 16.2

    RowLimit = 65536
    debit = 0: credit = 0
    For i = 0 To 7
        total(i) = Chr(i + 71)
    Next i

    '令count1=全表最後一列(有下粗框線)
    If Range("工作底稿!E8").End(xlDown).Row < RowLimit Then
        count1 = Range("工作底稿!E8").End(xlDown).Row + 3
    Else
        count1 = 8
        Do Until Range("工作底稿!F" & count1) = ""
            count1 = count1 + 1
        Loop
        count1 = count1 + 2
    End If

    '清除框線、格式、內容
    Range("工作底稿!E8:N" & count1).Clear

    '調整上下窄列
    Rows(count1 - 2 & ":" & count1 - 2).RowHeight = 16.2
    Rows(count1 & ":" & count1).RowHeight = 16.2
    '初始化完畢


    '令count1=科目表最後一列
    If Range("會計科目表!D6").End(xlDown).Row < RowLimit Then
        count1 = Range("會計科目表!D6").End(xlDown).Row
    Else
        count1 = 6
        Do Until Range("會計科目表!D" & count1) = ""
            count1 = count1 + 1
        Loop
        count1 = count1 - 1
    End If

    '從工作底稿第8列開始擺放資料
    r = 8

    '有初始餘額的科目先擺進來
    For i = 6 To count1
        If Range("會計科目表!G" & i) <> "" Or Range("會計科目表!H" & i) <> "" Then
            '擺編號
            Range("工作底稿!E" & r) = Range("會計科目表!D" & i)
            '擺科目名稱
            Range("工作底稿!F" & r) = Range("會計科目表!E" & i)
            '在總額擺初始借方
            Range("工作底稿!G" & r) = Range("會計科目表!G" & i)
            '在總額擺初始貸方
            Range("工作底稿!H" & r) = Range("會計科目表!H" & i)
            r = r + 1
        End If
    Next i
    '跑完後，r=空白的新一列

    '將日記簿有紀錄的編號、科目擺進來
    For i = 6 To count1
        find = 0

        '在1~12頁尋找
        For j = 0 To 11

            '若該科目有初始餘額，不必再重複擺放
            If Range("會計科目表!G" & i) <> "" Or Range("會計科目表!H" & i) <> "" Then
                Exit For
            Else
                '令count2=日記簿該頁最後一列
                If Range("日記簿!" & column(j, 0) & 10).End(xlDown).Row < RowLimit Then
                    count2 = Range("日記簿!" & column(j, 0) & 10).End(xlDown).Row
                Else
                    count2 = 10
                    Do Until Range("日記簿!" & column(j, 0) & count2) = ""
                        count2 = count2 + 1
                    Loop
                    count2 = count2 - 1
                End If

                'k=正在搜尋日記簿的列
                For k = 10 To count2

                    '找到科目後擺進工作底稿
                    If Range("日記簿!" & column(j, 0) & k) = Range("會計科目表!E" & i) Or Range("日記簿!" & column(j, 0) & k) = "　　" & Range("會計科目表!E" & i) Then
                        '擺編號
                        Range("工作底稿!E" & r) = Range("會計科目表!D" & i)
                        '擺科目名稱
                        Range("工作底稿!F" & r) = Range("會計科目表!E" & i)
                        '指向工作底稿下一列
                        r = r + 1
                        find = 1
                        Exit For
                    End If
                Next k
                '若找到科目就跳出j迴圈，不必再往下一頁找
                If find = 1 Then Exit For
            End If
    Next j, i
    '到此就知道工作底稿要擺入哪些科目的資料

    '設定本期損益的編號並加上文字
    Range("工作底稿!E" & r) = 999
    Range("工作底稿!F" & r) = "本期損益"
    '設定合計聯合大框
    Rows(r + 1 & ":" & r + 1).RowHeight = 3
    Range("工作底稿!E" & r + 2) = "合計"
    Rows(r + 3 & ":" & r + 3).RowHeight = 3


    '令r=工作底稿最後科目所在列
    r = r - 1

    'i=工作底稿科目列
    For i = 8 To r

        '日記簿1~12頁
        For j = 0 To 11

            '令count1=日記簿該頁最後一列
            If Range("日記簿!" & column(j, 0) & 10).End(xlDown).Row < RowLimit Then
                count1 = Range("日記簿!" & column(j, 0) & 10).End(xlDown).Row
            Else
                count1 = 10
                Do Until Range("日記簿!" & column(j, 0) & count1) = ""
                    count1 = count1 + 1
                Loop
                count1 = count1 - 1
            End If

            'k=日記簿正在搜尋的列
            For k = 10 To count1
                If Range("日記簿!" & column(j, 0) & k) = Range("工作底稿!F" & i) Or Range("日記簿!" & column(j, 0) & k) = "　　" & Range("工作底稿!F" & i) Then
                    debit = debit + Range("日記簿!" & column(j, 1) & k)
                    credit = credit + Range("日記簿!" & column(j, 2) & k)
                End If
        Next k, j
        '離開j迴圈代表該科目累積完日記簿的總額了

        '放入總額試算表的借貸方
        Range("工作底稿!G" & i) = Range("工作底稿!G" & i) + debit
        Range("工作底稿!H" & i) = Range("工作底稿!H" & i) + credit

        '由總額算出餘額，並放入
        If Range("工作底稿!G" & i) > Range("工作底稿!H" & i) Then
            Range("工作底稿!I" & i) = Range("工作底稿!G" & i) - Range("工作底稿!H" & i)
        Else
            Range("工作底稿!J" & i) = Range("工作底稿!H" & i) - Range("工作底稿!G" & i)
        End If

        '科目編號1~3開頭應放入資產負債表
        If Left(Range("工作底稿!E" & i), 1) <= 3 Then
            Range("工作底稿!M" & i) = Range("工作底稿!I" & i)
            Range("工作底稿!N" & i) = Range("工作底稿!J" & i)
        '否則放入綜合損益表
        Else
            Range("工作底稿!K" & i) = Range("工作底稿!I" & i)
            Range("工作底稿!L" & i) = Range("工作底稿!J" & i)
        End If

        '將本科目已累積的借貸總額歸零
        debit = 0: credit = 0
    Next i
    '到此各科目的金額都填寫完畢

    '填寫損益表本期損益
    For i = 8 To r
        debit = debit + Range("工作底稿!K" & i)
        credit = credit + Range("工作底稿!L" & i)
    Next i
    If debit > credit Then
        Range("工作底稿!L" & r + 1) = debit - credit
    Else
        Range("工作底稿!K" & r + 1) = credit - debit
    End If

    '填寫資產負債表本期損益
    debit = 0: credit = 0
    For i = 8 To r
        debit = debit + Range("工作底稿!M" & i)
        credit = credit + Range("工作底稿!N" & i)
    Next i
    If debit > credit Then
        Range("工作底稿!N" & r + 1) = debit - credit
    Else
        Range("工作底稿!M" & r + 1) = credit - debit
    End If

    '依照科目編號排序
    With ActiveWorkbook.Worksheets("工作底稿").Sort
        .SetRange Range("E8:N" & r)
        .Header = xlNo
        .MatchCase = False
        .Orientation = xlTopToBottom
        .SortMethod = xlPinYin
        .Apply
    End With

    'r+1=本期損益所在列
    'r+3=合計所在列

    '輸入合計列SUM公式
    For i = 0 To 7
        Range("工作底稿!" & total(i) & r + 3) = "=SUM(" & total(i) & "8:" & total(i) & r + 1 & ")"
    Next i

    '將0改為null
    For i = 8 To r + 3
        If Range("工作底稿!G" & i) = 0 Then Range("工作底稿!G" & i) = ""
        If Range("工作底稿!H" & i) = 0 Then Range("工作底稿!H" & i) = ""
        If Range("工作底稿!I" & i) = 0 Then Range("工作底稿!I" & i) = ""
        If Range("工作底稿!J" & i) = 0 Then Range("工作底稿!J" & i) = ""
        If Range("工作底稿!K" & i) = 0 Then Range("工作底稿!K" & i) = ""
        If Range("工作底稿!L" & i) = 0 Then Range("工作底稿!L" & i) = ""
        If Range("工作底稿!M" & i) = 0 Then Range("工作底稿!M" & i) = ""
        If Range("工作底稿!N" & i) = 0 Then Range("工作底稿!N" & i) = ""
    Next i

    '套用千分位(無小數點)樣式
    Range("工作底稿!G8:N" & r + 4).Style = "Comma"
    Range("工作底稿!G8:N" & r + 4).NumberFormatLocal = "_-* #,##0_-;-* #,##0_-;_-* ""-""??_-;_-@_-"

    '整個工作表設定微軟正黑體
    With Cells.Font
        .Name = "微軟正黑體"
        .Size = 12
        .Strikethrough = False
        .Superscript = False
        .Subscript = False
        .OutlineFont = False
        .Shadow = False
        .Underline = xlUnderlineStyleNone
        .ThemeColor = xlThemeColorLight1
        .TintAndShade = 0
        .ThemeFont = xlThemeFontNone
    End With

    '科目編號置中
    With Range("E8", "E" & r + 1)
        .HorizontalAlignment = xlCenter
        .VerticalAlignment = xlCenter
    End With

    '合計聯合大框跨欄置中
    With Range("工作底稿!E" & r + 3 & ":F" & r + 3)
        .HorizontalAlignment = xlCenter
        .VerticalAlignment = xlCenter
        .WrapText = False
        .Orientation = 0
        .AddIndent = False
        .IndentLevel = 0
        .ShrinkToFit = False
        .ReadingOrder = xlContext
        .MergeCells = False
    End With
    Range("工作底稿!E" & r + 3 & ":F" & r + 3).Merge

    '對上方設框線
    With Range("工作底稿!E8:N" & r + 1).Borders(xlEdgeLeft)
        .LineStyle = xlContinuous
        .ColorIndex = xlAutomatic
        .TintAndShade = 0
        .Weight = xlThick
    End With
    Range("工作底稿!E8:N" & r + 1).Borders(xlEdgeTop).LineStyle = xlNone
    With Range("工作底稿!E8:N" & r + 1).Borders(xlEdgeBottom)
        .LineStyle = xlContinuous
        .ColorIndex = xlAutomatic
        .TintAndShade = 0
        .Weight = xlThin
    End With
    With Range("工作底稿!E8:N" & r + 1).Borders(xlEdgeRight)
        .LineStyle = xlContinuous
        .ColorIndex = xlAutomatic
        .TintAndShade = 0
        .Weight = xlThick
    End With
    With Range("工作底稿!E8:N" & r + 1).Borders(xlInsideVertical)
        .LineStyle = xlContinuous
        .ColorIndex = xlAutomatic
        .TintAndShade = 0
        .Weight = xlThin
    End With
    With Range("工作底稿!E8:N" & r + 1).Borders(xlInsideHorizontal)
        .LineStyle = xlContinuous
        .ColorIndex = xlAutomatic
        .TintAndShade = 0
        .Weight = xlThin
    End With

    '對下方設框線
    With Range("工作底稿!E" & r + 2 & ":N" & r + 4).Borders(xlEdgeLeft)
        .LineStyle = xlContinuous
        .ColorIndex = xlAutomatic
        .TintAndShade = 0
        .Weight = xlThick
    End With
    With Range("工作底稿!E" & r + 2 & ":N" & r + 4).Borders(xlEdgeBottom)
        .LineStyle = xlContinuous
        .ColorIndex = xlAutomatic
        .TintAndShade = 0
        .Weight = xlThick
    End With
    With Range("工作底稿!E" & r + 2 & ":N" & r + 4).Borders(xlEdgeRight)
        .LineStyle = xlContinuous
        .ColorIndex = xlAutomatic
        .TintAndShade = 0
        .Weight = xlThick
    End With
    With Range("工作底稿!E" & r + 2 & ":N" & r + 4).Borders(xlInsideVertical)
        .LineStyle = xlContinuous
        .ColorIndex = xlAutomatic
        .TintAndShade = 0
        .Weight = xlThin
    End With
    Range("工作底稿!E" & r + 2 & ":N" & r + 4).Borders(xlInsideHorizontal).LineStyle = xlNone

    '消除在合計斷掉的垂直線
    Range("工作底稿!F" & r + 2).Borders(xlEdgeLeft).LineStyle = xlNone
    Range("工作底稿!F" & r + 4).Borders(xlEdgeLeft).LineStyle = xlNone

    '加減線、終結線
    With Range("工作底稿!G" & r + 3 & ":N" & r + 3).Borders(xlEdgeTop)
        .LineStyle = xlContinuous
        .Color = -16776961
        .TintAndShade = 0
        .Weight = xlThin
    End With
    With Range("工作底稿!G" & r + 3 & ":N" & r + 3).Borders(xlEdgeBottom)
        .LineStyle = xlDouble
        .Color = -16776961
        .TintAndShade = 0
        .Weight = xlThick
    End With

    End Sub
